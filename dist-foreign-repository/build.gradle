apply plugin: 'rup-jdbc'
apply plugin: 'rup-mybatis'

dependencies {
    compile project(':dist-foreign-domain')

    compile("commons-collections:commons-collections:${commonsCollectionsVersion}")
    compile("org.perf4j:perf4j:${perf4jVersion}")
    compile("com.copyright.rup.dist.common:dist-common-repository:${distCommonVersion}")

    compile("org.mybatis:mybatis-typehandlers-jsr310:${mybatisTypehandlersVersion}")
    compile("net.sf.supercsv:super-csv:${superCsvVersion}")

    testIntegRuntime project(":dist-foreign-db")
    testIntegRuntime("com.copyright.rup.common:rup-common-test-integ:${rupCommonTestIntegVersion}")
    testIntegRuntime(group: "com.copyright.rup.common", name: "rup-common-test-integ",
            version: "${rupCommonTestIntegVersion}", configuration: "postgres")
}

String pidLocation = "${projectDir}/build/tmp/embeddedPostgres/postgres.pid"

integrationTest.doFirst {
    systemProperty "dbPidLocation",pidLocation
}

//This hook used for closing PostgreSQL processes when integrationTest task was closed early
Runtime.runtime.addShutdownHook({
    File file = new File(pidLocation)
    if (file.exists()) {
        String pid = file.text
        if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
            Runtime.runtime.exec("Taskkill /PID " + pid + " /F")
        } else if (org.gradle.internal.os.OperatingSystem.current().isLinux() ||
                org.gradle.internal.os.OperatingSystem.current().isMacOsX()) {
            Runtime.runtime.exec("kill -9 " + pid)
        }
        file.deleteOnExit()
    }
})
