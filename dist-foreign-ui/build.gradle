import fi.jasoft.plugin.GradleVaadinPlugin
import fi.jasoft.plugin.tasks.CompileThemeTask

apply plugin: 'rup-war-ui'
apply plugin: 'rup-tomcat'

if (!project.plugins.findPlugin(GradleVaadinPlugin)) {
    project.apply(plugin: GradleVaadinPlugin)
}

vaadin {
    version = "${vaadinVersion}"
    manageDependencies = false
    manageRepositories = false
}

dependencies {
    compile project(':dist-foreign-service')

    compile("com.copyright.rup.vaadin:rup-vaadin-core:${rupVaadinVersion}")
    compile("com.copyright.rup.vaadin:rup-vaadin-theme:${rupVaadinVersion}")
    compile("com.vaadin:vaadin-spring:${vaadinSpringVersion}")

    runtime("com.copyright.rup.common:rup-common-config:${rupCommonVersion}")
    runtime("com.copyright.rup.common:rup-common-web:${rupCommonVersion}")
    runtime("com.copyright.rup.security.auth:rup-auth-consumer:${rupAuthVersion}")
    runtime("com.vaadin:vaadin-push:${vaadinVersion}")
    runtime("org.codehaus.janino:janino:${janinoVersion}")
    runtime("org.springframework.security:spring-security-core:${springSecurityVersion}")

    testCompile("com.copyright.rup.dist.common:dist-common-test:${distCommonVersion}")
    testCompile("org.apache.commons:commons-text:${commonsTextVersion}")

    testRuntime("cglib:cglib-nodep:${cglibNodepVersion}")

    testIntegRuntime("com.copyright.rup.common:rup-common-config:${rupCommonVersion}")
    testIntegRuntime("com.copyright.rup.common:rup-common-web:${rupCommonVersion}")
    testIntegRuntime(group: "com.copyright.rup.common", name: "rup-common-test-integ", version: "${rupCommonVersion}", configuration: "postgres")
}

task compileVaadinTheme(type: CompileThemeTask, group: "rupVaadin")

war {
    dependsOn 'compileVaadinTheme'
    exclude '**/*.scss'
}

afterEvaluate {
    appBeforeIntegrationTest.doFirst {
        gretty {
            integrationTestTask = 'integrationTest'
            httpPort = 18321
            //TODO: find the way how we can add testInteg classes and resources without copy
            copy {
                from "${projectDir}/build/resources/testInteg/"
                into "${projectDir}/build/resources/main/"
            }
            copy {
                from "${projectDir}/build/classes/testInteg/"
                into "${projectDir}/build/classes/main/"
            }
            copy {
                from "${projectDir}/src/testInteg/webapp/"
                into "${projectDir}/build/inplaceWebapp/"
            }
            systemProperty 'APP_LOG_DIR', "${projectDir}/build/logs/integrationTest"
        }
    }
    integrationTest.dependsOn appBeforeIntegrationTest
    appBeforeIntegrationTest.debug = (System.getProperty("integrationTestDebug") ?: 'false').toBoolean()
    appBeforeIntegrationTest.dependsOn(":dist-foreign-ui:testIntegClasses")
}
